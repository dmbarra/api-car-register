buildscript {
	ext {
		springBootVersion = '1.5.2.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'

jar {
	baseName = 'car-api'
	version = '0.0.1-SNAPSHOT'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
}

configurations {
	cucumberRuntime {
		extendsFrom testRuntime
	}
}

task cucumber() {
	dependsOn assemble, compileTestJava
	doLast {
		javaexec {
			main = "cucumber.api.cli.Main"
			classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
			args = ['-f', 'pretty', '--glue', 'src/test/groovy/component/steps', 'src/test/resources/features']
		}
	}
}

test {
	useTestNG() {
	}
}

dependencies {

	cucumberRuntime files("${jar.archivePath}")

	compile('org.springframework.boot:spring-boot-starter-actuator')
	compile("org.springframework.boot:spring-boot-starter-web") {
		exclude module: "spring-boot-starter-tomcat"
	}
	compile("org.springframework.boot:spring-boot-starter-jetty")
	compile('org.codehaus.groovy:groovy-all:2.4.9')

	testCompile('org.assertj:assertj-core:3.6.2')
	testCompile('org.testng:testng:6.9.13.6')
	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile('info.cukes:cucumber-junit:1.1.8')
	testCompile('info.cukes:cucumber-groovy:1.2.5')
}
